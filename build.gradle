plugins {
    id 'org.springframework.boot' version '2.5.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'idea'
    id 'jacoco'
    id "org.sonarqube" version "4.4.1.3373"
}

group 'ch.uzh.ifi.hasel'
version '1.0.0'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

repositories {
    mavenCentral()
}

springBoot {
    mainClass.set('ch.uzh.ifi.hase.soprafs24.Application')
}

dependencies {
    implementation 'org.mapstruct:mapstruct:1.3.1.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.3.1.Final'
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.3.1.Final'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.springframework.boot:spring-boot-starter-webflux:2.5.5'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.webjars.npm:sockjs-client:1.5.0'

    //MessageSecurityMetadataSourceRegistry
    implementation 'org.springframework.security:spring-security-messaging'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'

    // Spring Boot Test Starter (includes JUnit Jupiter)
    testImplementation ('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        exclude group: 'org.mockito', module: 'mockito-core'
    }
    testImplementation 'org.springframework.security:spring-security-test'

    // JUnit 5 Jupiter API and Engine
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    
    // Mockito
    testImplementation 'org.mockito:mockito-junit-jupiter:3.6.0' // Integrates Mockito with JUnit 5
    testImplementation 'org.mockito:mockito-core:3.6.0' // Core Mockito functionality

    // AssertJ
    testImplementation 'org.assertj:assertj-core:3.18.1' // Keeping AssertJ updated
}


bootJar {
    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
}


sonar {
  properties {
    property "sonar.projectKey", "sopra-fs24-group-27_sopra-fs24-group-27-server"
    property "sonar.organization", "sopra-fs24-group-27"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

File secretPropsFile = file('./local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
}

defaultTasks 'bootJar', 'build'
